buildscript {
    dependencies {
        classpath depends.proguard_gradle
    }
    repositories {
        mavenCentral()
    }
}

dependencies {
    // SmaliVM
    compile project(':smalivm')

    // Lazy
    compile depends.guava

    // CLI parsing
    compile 'commons-cli:commons-cli:1.3.1'

    // Primitive collections
    compile depends.trove4j

    // Logging
    compile depends.slf4j_api
    compile 'ch.qos.logback:logback-core:1.1.7'
    compile 'ch.qos.logback:logback-classic:1.1.7'

    // Testing
    testCompile depends.junit
    testCompile depends.mockito
    testCompile depends.perfidix // benchmarks
    testCompile project(path: ':smalivm', configuration: 'testArtifacts')
}

// Because of conflicting transitive dependencies (commons-cli), SmaliVM must be at the END of the classpath.
eclipse.classpath.file {
    withXml {
        def node = it.asNode()
        def smalivmNode
        node.children().each { n ->
            if ( n.attributes()['path'] == '/smalivm') {
                smalivmNode = n
                return
            }
        }
        node.remove(smalivmNode)
        node.append(smalivmNode)
    }
}

// Build a separate jar that contains all dependencies
task fatJar(type: Jar) {
    from sourceSets.main.output
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

    manifest {
        attributes('Main-Class': 'org.cf.simplify.Main')
    }

    doLast {
        if (!System.getProperty('os.name').toLowerCase().contains('windows')) {
            ant.symlink(link: file("${destinationDir}/simplify.jar"), resource: archivePath, overwrite: true)
        }
    }
}
tasks.getByPath('build').dependsOn(fatJar)

task proguard(type: proguard.gradle.ProGuardTask, dependsOn: fatJar) {
    def outFile = fatJar.destinationDir.getPath() + '/' + fatJar.baseName + '-' + fatJar.version + '-proguard' + '.' + fatJar.extension

    injars fatJar.archivePath
    outjars outFile

    libraryjars "${System.properties['java.home']}/lib/rt.jar"

    dontobfuscate
    //dontoptimize
    optimizations '!code/allocation/variable'

    keepclassmembers 'enum * { public static **[] values(); public static ** valueOf(java.lang.String); }'
    keepattributes '*Annotation*,Signature,InnerClasses,EnclosingMethod'

    keep 'public class org.cf.simplify.Main { public static void main(java.lang.String[]); }'
    keep 'public class android.util.**  { *; }'

    keep 'class org.apache.commons.logging.** { public protected *; }'
    keep 'public class ch.qos.** { public protected *; }'
    keep 'public class org.slf4j.** { public protected *; }'

    dontwarn
    verbose
}
